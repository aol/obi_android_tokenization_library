apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'signing'

group = "com.aol.obi"
archivesBaseName = "android-tokenization-lib"
version = "1.1"

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 2
        versionName "1.02"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main {
            java {
                srcDir 'src/main/java'
            }
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
}

task clearJar(type: Delete) {
    delete 'build/libs/CardEncryptionLib.jar'
}

build {
  doFirst {
   copy {
     from('README.md')
     into('build/intermediates/classes/release/META-INF/')
    }
  }
}

task makeJar(type: Jar) {
      from('build/intermediates/classes/release/')
      archiveName 'android-tokenization-lib.jar'
}

makeJar.dependsOn(clearJar)

task javadoc(type: Javadoc) {
    failOnError false
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.compile
}

// build a jar with javadoc
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'

    include("**/EncryptCard.java")
}

artifacts {
    archives makeJar, javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}


uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      pom.project {
        name 'OBI Tokenization Android Library'
        packaging 'jar'
        // optionally artifactId can be defined here 
        description 'This library can be used to tokenize sensitive information'
        url 'https://github.com/aol/obi_android_tokenization_library'

        scm {
          connection 'scm:git@github.com:aol/obi_android_tokenization_library.git'
          developerConnection 'scm:git@github.com:aol/obi_android_tokenization_library.git'
          url 'https://github.com/aol/obi_android_tokenization_library'
        }

        licenses {
          license {
            name 'Private / Commercial'
          }
        }

        developers {
          developer {
            id 'jagadeeshaluri00'
            name 'Jagadeesh Aluri'
            email 'Jagadeesh.Aluri@teamaol.com'
          }
        }
      }
    }
  }
}
